tree("Root")
	parallel
		repeat mute tree("BeAlive")
		repeat mute tree("Die")

tree("BeAlive")
	fallback
		tree("Confrontation")
		tree("ChaseEnemy")
		tree("Idle")

tree("Confrontation")
	sequence
		HasTarget
		IsTargetInRange(9.0)
		GenerateAITransition	
		RunToWalk
		fallback
			sequence
				CheckInIdleState 
				IsTargetInRange(5.0)
				CheckAITransitionInRange(0.0, 0.5)
				tree("Execute")
			sequence
				CheckInIdleState 
				IsTargetInRange(2.0)
				CheckAITransitionInRange(0.0, 0.7)
				tree("Attack")
			sequence
				CheckAITransitionInRange(0.0, 0.85)
				tree("Surrounding")
			sequence
				StopMove
				ResetAITransition
				Wait(0.5)
						
				
				


tree("ChaseEnemy")
	sequence
		HasTarget
		not IsTargetInRange(9.0)
		WalkToRun
		SetInCrowd(false)
		KeepMove(5.0)
			

tree("Surrounding")
	sequence
		IsTargetInRange(9.0)
		SetInCrowd(true)
		ResetAITransition
		BeginMove	
		Wait(1.0)
		StopMove
	


tree("Idle")
	while
		sequence
			not HasTarget
			not FindTarget

		repeat
			Wait(1.0)


tree("Attack")
	sequence
		StopMove
		WaitRandom(0.2, 0.4)
		SetInputVectorToFaceTarget 
		ResetAITransition
		AttackCommandInput
		Wait(0.20)
		AttackCommandCancel

tree("Execute")
	sequence
		StopMove
		SetInputVectorToFaceTarget 
		ResetAITransition
		ExecuteCommandInput
		Wait(0.20)
		ExecuteCommandCancel


tree("Die")
	IsDead