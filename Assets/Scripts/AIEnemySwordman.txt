tree("Root")
	parallel
		repeat mute tree("BeAlive")
		repeat mute tree("Die")

tree("BeAlive")
	fallback
		tree("Confrontation")
		tree("HitReaction")
		tree("ChaseEnemy")
		tree("Idle")

tree("Confrontation")
	while not GetHit
		sequence
			HasTarget
			IsTargetInRange(6.0)
			RunToWalk
			SetInCrowd(true)
			GenerateAITransition	
			fallback
				sequence
					CheckInIdleState 
					CheckAITransitionInRange(0.0, 0.85)
					tree("Surrounding")
				sequence
					CheckInIdleState 
					IsTargetInRange(5.0)
					StopMove
					Wait(1.0)
				
				
tree("HitReaction")
	while GetHit
		sequence
			WaitForForcedDodgeTransition 
			GuardNotInCooldown
			IsTargetInRange(2.5)
			GenerateAITransition	
			CheckAITransitionInRange(0.0, 0.7)
			tree("Guard")

tree("ChaseEnemy")
	sequence
		HasTarget
		not IsTargetInRange(6.0)
		GenerateAITransition	
		fallback
			sequence
				//Wait(1.0)
				CheckInIdleState 
				IsTargetInFacingRange(90.0)
				not IsTargetInRange(2.0)
				CheckAITransitionInRange(0.0, 0.15)
				ResetAITransition
				tree("Execute")
			sequence
				//CheckAITransitionInRange(0.0, 0.15)
				WalkToRun
				SetInCrowd(false)
				BeginMove	
				Wait(0.5)
				//KeepMove(3.0)
			

tree("Surrounding")
	parallel
		repeat
			mute
				sequence
					CheckInIdleState
					IsTargetInRange(9.0)
					BeginMove	
		repeat
			mute
				sequence
					CheckInIdleState
					GuardNotInCooldown
					LastTargetAttackTimeLessThan(0.5)
					IsTargetFacingSelf(60.0)
					IsTargetInRange(2.5)
					GenerateAITransition	
					CheckAITransitionInRange(0.0, 0.7)
					tree("Guard")
		repeat
			mute
				sequence
					Wait(0.5)
					CheckInIdleState 
					IsTargetInRange(2.5)
					GenerateAITransition	
					CheckAITransitionInRange(0.0, 0.7)
					tree("Attack")
		repeat
			mute
				sequence
					Wait(0.5)
					CheckInIdleState 
					not IsTargetInRange(2.0)
					IsTargetInFacingRange(90.0)
					GenerateAITransition	
					CheckAITransitionInRange(0.0, 0.10)
					tree("Execute")
		sequence
			Wait(3.0)
			ResetInputTask
			Fail
			
			
	


tree("Idle")
	while
		sequence
			not HasTarget
			not FindTarget

		repeat
			Wait(1.0)


tree("Attack")
	//while not GetHit
	sequence
		StopMove
		WaitRandom(0.2, 0.4)
		SetInputVectorToFaceTarget 
		AttackCommandInput
		Wait(0.20)
		AttackCommandCancel
		ResetInputTask
		WaitForTransitionNotLessThan (1)
		sequence
			GenerateAITransition	
			CheckAITransitionInRange(0.0, 0.5)
			AttackCommandInput
			Wait(0.20)
			AttackCommandCancel
			ResetInputTask
			WaitForTransitionNotLessThan (1)

tree("Execute")
	//while not GetHit
	sequence
		StopMove
		SetInputVectorToFaceTarget 
		ExecuteCommandInput
		Wait(0.20)
		ExecuteCommandCancel
		ResetInputTask
		WaitForTransitionNotLessThan (1)
		RunToWalk
		Wait(0.50)

tree("Guard")
	//while not GetHit
	sequence
		StopMove
		SetInputVectorToFaceTarget 
		GuardCommandInput
		Wait(0.20)
		GuardCommandCancel
		ResetInputTask
		WaitForTransitionNotLessThan (1)

tree("Die")
	IsDead